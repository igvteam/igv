// The MIT License (MIT)
//
// Copyright (c) 2007-2018 Broad Institute
//
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
//
// The above copyright notice and this permission notice shall be included in
// all copies or substantial portions of the Software.
//
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
// THE SOFTWARE.
apply plugin: 'java'
import org.apache.tools.ant.filters.ReplaceTokens

ext.moduleName = 'org.broad.igv'
ext.mainClassName = 'org.broad.igv.ui.Main'

dependencies {
    compile fileTree(dir: 'lib', include: '*.jar')
    testCompile fileTree(dir: 'test/lib', include: '*.jar')
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.4.1'
}

sourceCompatibility = 1.9
targetCompatibility = 1.9

processResources {
    // Set the current timestamp for release builds; pass in a blank property to enable (i.e. -Ptimestamp="" at CLI)
    if (timestamp.equals("")) { timestamp = new Date().format("MM/dd/yyyy hh:mm aa") }

    filesMatching('resources/about.properties') {
        filter (ReplaceTokens,
                tokens: [ VERSION: version, BUILD: buildNumber, DEFAULT_MASTER_RESOURCE_URL: dataServerUrl,
                          TIMESTAMP: timestamp, DEVELOPMENT: development ])
    }

    from("src/main/resources-jlfgr-1_0") exclude "LICENSE", "META-INF"
}

compileJava {
    inputs.property("moduleName", moduleName)
    doFirst {
        options.compilerArgs = [
            '--module-path', files(file('modules')).asPath,
        ]
    }
}

FileCollection testModulePath = files(sourceSets.main.java.outputDir, file('modules'), file('test/modules'))

compileTestJava {
    inputs.property("moduleName", moduleName)
    doFirst {
        options.compilerArgs = [
            '--module-path',  testModulePath.asPath, 
            '--add-modules', 'junit',
            '--add-modules', 'fest.swing',
            '--patch-module', "$moduleName=" + files(sourceSets.test.java.srcDirs).asPath, 
        ]
        classpath = fileTree(dir: 'test/lib', include: '*.jar')
    }
}

tasks.withType(Test) {
    systemProperties = System.getProperties()
    systemProperties['java.awt.headless'] = 'true'
    systemProperties['make.fail'] = 'false'
    systemProperties['include.longrunning'] = 'false'
    systemProperties['ignore.ioexceptions'] = 'false'
    maxHeapSize = '2000m'
    jvmArgs '-javaagent:test/lib/JavaAgent.jar'
    forkEvery = 1
}

test {
    inputs.property("moduleName", moduleName)
    doFirst {
        jvmArgs = [
            '--module-path',  testModulePath.asPath, 
            '--add-modules', 'junit', 
            '--add-modules', 'fest.swing', 
            '--add-modules', 'java.xml.bind', 
            '--add-modules', 'ALL-MODULE-PATH',
            '--add-reads', "$moduleName=junit", 
            '--add-reads', "$moduleName=fest.swing",
            '--add-exports', "$moduleName/org.broad.igv.preprocess=junit",
            '--add-exports', "$moduleName/org.broad.tribble=junit",
            '--add-exports', "$moduleName/util=junit",
            '--patch-module', "$moduleName=" + files(sourceSets.test.java.outputDir, sourceSets.main.output.resourcesDir).asPath, 
        ]
        classpath = fileTree(dir: 'test/lib', include: '*.jar') + fileTree(dir: 'lib', include: '*.jar')
    }
}

jar {
    manifest {
        attributes(
            "Permissions": "all-permissions",
            "Application-Name": "IGV",
            "Built-By": System.getProperty('user.name'),
            "Main-Class": mainClassName,
            "Class-Path": configurations.compile.collect { it.getName() }.join(' ')
        )
    }
}

def classpathContent = copySpec {
    from ('lib') {
        include '*.jar'
    }
    into "lib"
}

def modulepathContent = copySpec {
    from ("${buildDir}/libs") {
        include '*.jar'
    }
    from ('modules')  {
        include '*.jar'
    }
    into "modules"
}

task createDist(type: Copy, dependsOn: jar)  {
    from ('scripts') {
        include 'igv.bat'
        include 'igv.command'
        include 'igv.sh'
        include 'igv.args'
    }
    
    with classpathContent
    with modulepathContent

    into "${buildDir}/dist"
}

task createDistZip(type: Zip, dependsOn: createDist) {
    destinationDir = file("${buildDir}/distZip")
    baseName = "IGV"
    from "${buildDir}/dist"
    into createDistZip.baseName + "_${version}"

    doLast {
        project.exec { commandLine('chmod', '775', createDistZip.archivePath ) }
    }
}

build.dependsOn createDistZip
